package Cup;

import Expressions.*;
import Typing.ConstantTypeInfo;
import Typing.FunctionTypeInfo;
import SymbolTable.SymTabInfo;
import SymbolTable.SymbolTable;
import SymbolTable.IntegerSymTabInfo;
import SymbolTable.VariableSymTabInfo;
import SymbolTable.StringSymTabInfo;
import Typing.Types;
import Typing.VariableTypeInfo;
import CodeGeneration.CodeContainer;
import CodeGeneration.VarnameCreator;
import CodeGeneration.ThreeAddressCode;
import CodeGeneration.OpCodes;

import java.util.ArrayList;
import java.util.LinkedList;
import java.util.Arrays;

action code {:

  public SymbolTable scope = new SymbolTable();    
  ArrayList<ThreeAddressCode> program = new ArrayList<ThreeAddressCode>();
:}
   
parser code {:

	public VariableSymTabInfo GenerateVariable(Types type)
	{
	  String varname = VarnameCreator.getInstance().CreateName();
	  VariableSymTabInfo vsti = new VariableSymTabInfo(new VariableTypeInfo(type), varname);
	  return vsti;
	}
    public StringSymTabInfo CreateLabel()
    {
        String label = VarnameCreator.getInstance().CreateLabel();
        ConstantTypeInfo cti = new ConstantTypeInfo(Types.STRING);
        StringSymTabInfo falseLabelSymtabInfo = new StringSymTabInfo(cti, label);
        return falseLabelSymtabInfo;

    }
    public StringSymTabInfo CreateLabel(String name)
    {
        String label = VarnameCreator.getInstance().CreateLabel(name);
        ConstantTypeInfo cti = new ConstantTypeInfo(Types.STRING);
        StringSymTabInfo falseLabelSymtabInfo = new StringSymTabInfo(cti, label);
        return falseLabelSymtabInfo;

    }
    public StringSymTabInfo CreateFunctionLabel(String name)
    {
        // Create a label that indicates the beginning of this function
        String label = VarnameCreator.getInstance().CreateFunctionLabel(name);
        ConstantTypeInfo cti = new ConstantTypeInfo(Types.STRING);
        StringSymTabInfo beginFunLabelSymtabInfo = new StringSymTabInfo(cti, label);
        return beginFunLabelSymtabInfo;
    }
	/* Change the method report_error so it will display the line and
	   column of where the error occurred in the input as well as the
	   reason for the error which is passed into the method in the
	   String 'message'. */
	public void report_error(String message, Object info) {
   
		/* Create a StringBuffer called 'm' with the string 'Error' in it. */
		StringBuffer m = new StringBuffer("Error");
   
		/* Check if the information passed to the method is the same
		   type as the type java_cup.runtime.Symbol. */
		if (info instanceof java_cup.runtime.Symbol) {
			/* Declare a java_cup.runtime.Symbol object 's' with the
			   information in the object info that is being typecasted
			   as a java_cup.runtime.Symbol object. */
			java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
			/* Check if the line number in the input is greater or
			   equal to zero. */
			if (s.left >= 0) {                
				/* Add to the end of the StringBuffer error message
				   the line number of the error in the input. */
				m.append(" in line "+(s.left+1));
				/* Check if the column number in the input is greater
				   or equal to zero. */
				if (s.right >= 0)                    
					/* Add to the end of the StringBuffer error message
					   the column number of the error in the input. */
					m.append(", column "+(s.right+1));
			}
		}
   
		/* Add to the end of the StringBuffer error message created in
		   this method the message that was passed into this method. */
		m.append(" : "+message);
   
		/* Print the contents of the StringBuffer 'm', which contains
		   an error message, out on a line. */
		System.err.println(m);
	}
   
	/* Change the method report_fatal_error so when it reports a fatal
	   error it will display the line and column number of where the
	   fatal error occurred in the input as well as the reason for the
	   fatal error which is passed into the method in the object
	   'message' and then exit.*/
	public void report_fatal_error(String message, Object info) {
		report_error(message, info);
		System.exit(1);
	}


	

:};
   

   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  

   Terminals that have no value are listed first and then terminals
   that do have an value, in this case an integer value, are listed on
   the next line down. */
terminal           CHAR;
terminal           NEQUAL, EQUAL, GREATER, LESS, NOT;
terminal           TIMES, MINUS, DIVIDE, PLUS;
terminal           WRITE, READ;
terminal           LENGTH;
terminal           WHILE, DO;
terminal           QCHAR;
terminal           EOL;
terminal           INT;
terminal           IF, ELSE;
terminal           LPAR, RPAR, LBRACE, RBRACE, LBRACK, RBRACK;
terminal           RETURN;
terminal           ASSIGN, SEMICOLON, COMMA, DOT;
terminal           LOW, UMINUS;



terminal Integer   NUMBER;
terminal String    NAME;

non terminal Expression                exp;
non terminal Statement                 statement;
non terminal StatementList             statements, block;
non terminal FunctionDeclaration       fun_declaration;
non terminal BooleanExpression         bexp;
non terminal Declaration               declaration;
non terminal CodeContainer             var_declarations, var_declaration;
non terminal Program                   program, total;
non terminal ParameterList             pars;
non terminal Types                     type;

non terminal SymTabInfo                formal_par, lexp;
non terminal LinkedList<SymTabInfo>    formal_pars, formal_params_non_empty;

/* ------------ Precedence declarations ------------------------------------ */

precedence nonassoc LOW;
precedence nonassoc ELSE;
precedence left     GREATER, LESS, EQUAL, NEQUAL;
precedence left     PLUS, MINUS;
precedence left     TIMES, DIVIDE;
precedence left     UMINUS;
precedence left     DOT, LBRACK;
/* ----------------------------Grammar Section------------------------------ */
// The total rule only serves as a rule so we can execute code at the EOF.
// Parsing the EOF token gave me some trouble so I skipped it and found this dirty solution.
// It works however, so I'm leaving it in pace. The codeblock can be used to print the
// symbol table for example after a succesful parse.

total                   ::=   program:p                                  {:
                                                                             // total
                                                                             //System.out.println(p.toString());
                                                                             p.setSymbolTable(scope);
                                                                             RESULT = p;
                                                                         :}
                          ;
program                 ::=   program:ps declaration:p                   {:
                                                                             // program
                                                                             // We have a CodeContainer which contains our entire program at this point.
                                                                             // program is a LinkedList<CodeContainer> and declaration is a single CodeContainer.
                                                                             // program can be null as well.
                                                                             Program pgrm = new Program();
                                                                             pgrm.AddDeclarations(ps);
                                                                             pgrm.AddDeclaration(p);
                                                                             RESULT = pgrm;
                                                                         :}
						  | /* Empty */
						  ;


declaration             ::=  fun_declaration:fd                          {:
                                                                             // declaration
                                                                             RESULT = fd;
                                                                         :}
						  |  var_declaration:vd
						  ;

fun_declaration         ::=  type:rt NAME:n
                                                                         {: // fun_declaration
                                                                            // Create symboltable entry
                                                                            VariableSymTabInfo vsti = new VariableSymTabInfo(null, n);
                                                                            // We insert it in the current scope but we update values later
                                                                            scope.Insert(vsti);
                                                                            scope = scope.NewScope();
                                                                            scope.function = vsti;
                                                                         :}
                             LPAR formal_pars:ps RPAR
                                                                         {:
                                                                            // Attach the parameters to the type
                                                                            // of the function defining the current scope
                                                                            scope.function.typeInfo = new FunctionTypeInfo(new VariableTypeInfo(rt), ps);
																		 :}
					         block:b
					                                                     {:
                                                                            StringSymTabInfo funLabel = parser.CreateFunctionLabel(n);
					                                                        scope = scope.parent;
                                                                            FunctionDeclaration f = new FunctionDeclaration();
                                                                            f.setBeginLabel(funLabel);
                                                                            f.setBody(b);
                                                                            RESULT = f;
					                                                     :}
						   ;

formal_pars             ::=  formal_params_non_empty:ps                  {: // formal_pars
                                                                            RESULT = ps;
                                                                         :}
						  |
						  ;

formal_params_non_empty ::= formal_par:p COMMA formal_params_non_empty:ps     {:
                                                                                // formal_params_non_empty
                                                                                ps.addFirst(p);
                                                                                RESULT = ps;
                                                                              :}
                          | formal_par:p                                      {:
                                                                                RESULT = new LinkedList<SymTabInfo>(Arrays.asList(p));
                                                                              :}
                          ;

formal_par              ::= type:t NAME:n
                                                                         {:
                                                                            // formal_par
                                                                            VariableSymTabInfo vsti = new VariableSymTabInfo(new VariableTypeInfo(t), n);
                                                                            // The current scope is the function itself,
                                                                            // So we add this parameter to the scope..
                                                                            scope.Insert(vsti);
                                                                            RESULT = vsti;
                                                                         :}
						  ;

block                   ::= LBRACE                                       {:
                                                                            // block
                                                                            // We open a new block so we create a new scope
                                                                            //We close the scope and put it back as the parent.
                                                                            scope = scope.NewScope();
                                                                         :}
                            var_declarations statements:sts 
                            RBRACE                                       {:
                                                                            scope = scope.parent;
                                                                            RESULT = sts;
                                                                         :}
						  ;

var_declaration         ::= type:t NAME:n SEMICOLON                      {: 
                                                                            // var_declaration
                                                                            VariableSymTabInfo vsti = new VariableSymTabInfo(new VariableTypeInfo(t), n);
                                                                            scope.Insert(vsti); 
                                                                         :}
						  ;

var_declarations        ::= var_declaration var_declarations
						  | /* Nothing */
						  ;

type                    ::= INT                                          {: // type
                                                                            /* We create a new TypeInfo that holds that the value is an integer
																			 * We then attach this type to the value of 'type'
																			 * INT and FLOAT are simple types
																			 * Arrays and structs are complex types..
																			 */ 
																			RESULT = Types.INTEGER;
																		 :}
						  | CHAR
                                                                         {:
                                                                             RESULT = Types.CHAR;
                                                                         :}
						  ;

statements                  ::= statement:st SEMICOLON statements:sts    {:
                                                                            // statements
                                                                            // Append the statement to the statements
                                                                            RESULT = new StatementList(st);
                                                                            RESULT.AddStatements(sts);
                                                                         :}
							  | /* Nothing */
							  ;

statement                   ::= IF LPAR bexp:cond RPAR statement:cons        {:

                                                                             StringSymTabInfo falseLabel = parser.CreateLabel("False");
                                                                             StringSymTabInfo trueLabel  = parser.CreateLabel("True");
                                                                             StringSymTabInfo endLabel   = parser.CreateLabel("End");

                                                                             cond.setTrueLabel(trueLabel);
                                                                             cond.setFalseLabel(falseLabel);
                                                                             cond.setEndLabel(endLabel);
                                                                             cond.setTrueCode(cons);

                                                                             IfStatement iex = new IfStatement();
                                                                             iex.setBexp(cond);
                                                                             RESULT = iex;
                                                                         :}
								%prec LOW
                              | IF LPAR bexp:cond RPAR block:cons ELSE block:alt  {:

                                                                             StringSymTabInfo falseLabel = parser.CreateLabel("False");
                                                                             StringSymTabInfo trueLabel  = parser.CreateLabel("True");
                                                                             StringSymTabInfo endLabel   = parser.CreateLabel("End");

                                                                             cond.setTrueLabel(trueLabel);
                                                                             cond.setFalseLabel(falseLabel);
                                                                             cond.setEndLabel(endLabel);
                                                                             cond.setTrueCode(cons);
                                                                             cond.setFalseCode(alt);

                                                                             IfElseStatement iex = new IfElseStatement();
                                                                             iex.setBexp(cond);
                                                                             RESULT = iex;                                                                       
                                                                         :}
							  | WHILE LPAR exp RPAR statement
							  | lexp:tar ASSIGN exp:ex                   {:
                                                                            AssignStatement as = new AssignStatement(tar, ex);
									    //cc.AppendCode(tac);
									    //cc.symTabInfo = tar;
                                                                            //program.add(aex.ToThreeAddressCode());

                                                                            RESULT = as;
                                                                         :}
							  | RETURN exp:e                             {:
                                                                            // The variable that holds the result of the expression is
                                                                            // is held inside e.symTabInfo. So we pass that to the TAC constructor.
                                                                            ReturnStatement rs = new ReturnStatement(e);
                                                                            //program.add(re.ToThreeAddressCode());
                                                                            RESULT = rs;
                                                                         :}
							  | WRITE exp
							  | READ lexp
							  ;

lexp                    ::= NAME:n                                       {:
                                                                             // We should have already declared this name
                                                                             // so we simply look it up in the symbol table
                                                                             RESULT = scope.Lookup(n);
                                                                         :}
						  | lexp LBRACK exp RBRACK  // array access
						  ;
bexp                    ::= exp:e1 GREATER exp:e2                        {:
                                                                             // For now we take the type of the first expression (no typecheck)
                                                                             VariableSymTabInfo tempVariable = parser.GenerateVariable(Types.INTEGER);
                                                                             scope.Insert(tempVariable);

                                                                             // Create ArithmeticExpression
                                                                             BooleanExpression exp = new BooleanExpression(e1.getExpressionType(), 
                                                                                                                           OpCodes.A2GT, 
                                                                                                                           e1, e2,
                                                                                                                           tempVariable);
                                                                             //program.add(exp.ToThreeAddressCode());
                                                                             RESULT = exp;
                                                                         :}                                                                                
                          | exp:e1 LESS    exp:e2                        {:
                                                                             // For now we take the type of the first expression (no typecheck)
                                                                             VariableSymTabInfo tempVariable = parser.GenerateVariable(Types.INTEGER);
                                                                             scope.Insert(tempVariable);

                                                                             // Create ArithmeticExpression
                                                                             BooleanExpression exp = new BooleanExpression(e1.getExpressionType(), 
                                                                                                                           OpCodes.A2LT, 
                                                                                                                           e1, e2,
                                                                                                                           tempVariable);
                                                                             //program.add(exp.ToThreeAddressCode());
                                                                             RESULT = exp;
                                                                         :}   
                          | exp:e1 EQUAL   exp:e2                        {:
                                                                             // For now we take the type of the first expression (no typecheck)
                                                                             VariableSymTabInfo tempVariable = parser.GenerateVariable(Types.INTEGER);
                                                                             scope.Insert(tempVariable);

                                                                             // Create ArithmeticExpression
                                                                             BooleanExpression exp = new BooleanExpression(e1.getExpressionType(), 
                                                                                                                           OpCodes.A2EQ, 
                                                                                                                           e1, e2,
                                                                                                                           tempVariable);
                                                                             //program.add(exp.ToThreeAddressCode());
                                                                             RESULT = exp;
                                                                         :}    
                          | exp:e1 NEQUAL  exp:e2                        {:
                                                                             // For now we take the type of the first expression (no typecheck)
                                                                             VariableSymTabInfo tempVariable = parser.GenerateVariable(Types.INTEGER);
                                                                             scope.Insert(tempVariable);

                                                                             // Create ArithmeticExpression
                                                                             BooleanExpression exp = new BooleanExpression(e1.getExpressionType(), 
                                                                                                                           OpCodes.A2NEQ, 
                                                                                                                           e1, e2,
                                                                                                                           tempVariable);
                                                                             //program.add(exp.ToThreeAddressCode());
                                                                             RESULT = exp;
                                                                         :} 
                          ;                           
//As is customary, we assume that || and && are left-associative, and that || has lowest precedence, then &&, then ! .
exp                     ::= exp:e1 PLUS    exp:e2                        {:
                                                                             // For now we take the type of the first expression (no typecheck)
                                                                             VariableSymTabInfo tempVariable = parser.GenerateVariable(e1.getExpressionType());
                                                                             scope.Insert(tempVariable);

                                                                             // Create ArithmeticExpression
                                                                             BinaryArithmeticExpression exp = new BinaryArithmeticExpression(e1.getExpressionType(), 
                                                                                                                                 OpCodes.A2PLUS, 
                                                                                                                                 e1, e2,
                                                                                                                                 tempVariable);
                                                                             //program.add(exp.ToThreeAddressCode());
                                                                             RESULT = exp;
                                                                         :}  
						  | exp:e1 MINUS   exp:e2                        {:
                                                                             // For now we take the type of the first expression (no typecheck)
                                                                             VariableSymTabInfo tempVariable = parser.GenerateVariable(e1.getExpressionType());
                                                                             scope.Insert(tempVariable);

                                                                             // Create ArithmeticExpression
                                                                             BinaryArithmeticExpression exp = new BinaryArithmeticExpression(e1.getExpressionType(), 
                                                                                                                                 OpCodes.A2MINUS, 
                                                                                                                                 e1, e2,
                                                                                                                                 tempVariable);
                                                                             //program.add(exp.ToThreeAddressCode());
                                                                             RESULT = exp;
                                                                         :} 
						  | exp:e1 TIMES   exp:e2                        {:
                                                                             // For now we take the type of the first expression (no typecheck)
                                                                             VariableSymTabInfo tempVariable = parser.GenerateVariable(e1.getExpressionType());
                                                                             scope.Insert(tempVariable);

                                                                             // Create ArithmeticExpression
                                                                             BinaryArithmeticExpression exp = new BinaryArithmeticExpression(e1.getExpressionType(), 
                                                                                                                                 OpCodes.A2TIMES, 
                                                                                                                                 e1, e2,
                                                                                                                                 tempVariable);
                                                                             //program.add(exp.ToThreeAddressCode());
                                                                             RESULT = exp;
                                                                         :} 
						  | exp:e1 DIVIDE  exp:e2                        {:
                                                                             // For now we take the type of the first expression (no typecheck)
                                                                             VariableSymTabInfo tempVariable = parser.GenerateVariable(e1.getExpressionType());
                                                                             scope.Insert(tempVariable);

                                                                             // Create ArithmeticExpression
                                                                             BinaryArithmeticExpression exp = new BinaryArithmeticExpression(e1.getExpressionType(), 
                                                                                                                                 OpCodes.A2DIV, 
                                                                                                                                 e1, e2,
                                                                                                                                 tempVariable);
                                                                             //program.add(exp.ToThreeAddressCode());
                                                                             RESULT = exp;
                                                                         :} 
						  | LPAR exp:e RPAR                              {:
                                                                             RESULT = e;
                                                                         :}
						  | exp LBRACK exp RBRACK                        {:
                                                                            // Arrays

                                                                         :}
						  | MINUS exp:e                                  {:

                                                                             // For now we take the type of the first expression (no typecheck)
                                                                             VariableSymTabInfo tempVariable = parser.GenerateVariable(e.getExpressionType());
                                                                             scope.Insert(tempVariable);

                                                                             // Create ArithmeticExpression
                                                                             UnaryArithmeticExpression exp = new UnaryArithmeticExpression(e.getExpressionType(), 
                                                                                                                                 OpCodes.A1MINUS, 
                                                                                                                                 e,
                                                                                                                                 tempVariable);
                                                                             //program.add(exp.ToThreeAddressCode());
                                                                             RESULT = exp;
                                                                         :} 
                          | bexp:exp                                     {:
                                                                             RESULT = exp;                            
                                                                         :}
                            %prec UMINUS  
						  | NUMBER:n                                     {:

                                                                            ConstantTypeInfo ti = new ConstantTypeInfo(Types.INTEGER);
                                                                            IntegerSymTabInfo csti = new IntegerSymTabInfo(n.intValue());

                                                                            ConstantExpression ce = new ConstantExpression(csti, Types.INTEGER);
                                                                            RESULT = ce;
                                                                         :}
						  | NAME:n LPAR pars:ps RPAR                     {:
                                                                            // Function Call
                                                                            // Add each TAC for each parameter to the result.
                                                                            //CodeContainer temp = new CodeContainer()

                                                                            // Get the SymTabInfo from the scope
                                                                            VariableSymTabInfo func = (VariableSymTabInfo)scope.Lookup(n);
                                                                            // We need the return type, so we get it from the SymTabInfo.
                                                                            FunctionTypeInfo funcTypeInfo = (FunctionTypeInfo) func.typeInfo;

                                                                            VariableSymTabInfo tempVariable = parser.GenerateVariable(funcTypeInfo.ActualType());
                                                                            scope.Insert(tempVariable);

                                                                            FunctionCallExpression result = new FunctionCallExpression(tempVariable, funcTypeInfo.ActualType(), func, ps);
                                                                            // Create the TAC for the function call.
                                                                            //program.add(result.ToThreeAddressCode());
                                                                            RESULT = result;
                                                                         :}
						  | QCHAR
						  | NAME:n                                       {:
                                                                             VariableExpression var = new VariableExpression(scope.Lookup(n), scope.Lookup(n).typeInfo.ActualType());
                                                                             RESULT = var;
                                                                         :}
						  ;

pars                    ::= exp:p                                        {:
                                                                            RESULT = new ParameterList(p);
                                                                         :}
						  | exp:p COMMA pars:ps                          {:
                                                                            ParameterList result = new ParameterList(p);
                                                                            result.AddParameters(ps);
                                                                            RESULT = result;
                                                                         :}
						  | /* No parameters */
						  ;


