                                                                                                               package Cup;

import Typing.FunctionTypeInfo;
import Main.SymTabInfo;
import Main.SymbolTable;
import Typing.TypeInfo;
import Typing.Types;

import CodeGeneration.CodeContainer;
import CodeGeneration.VarnameCreator;
import CodeGeneration.ThreeAddressCode;
import CodeGeneration.OpCodes;

import java.util.LinkedList;
import java.util.Arrays;


action code {:

  SymbolTable scope = new SymbolTable();
:}
   
parser code {:
	
	/* Change the method report_error so it will display the line and
	   column of where the error occurred in the input as well as the
	   reason for the error which is passed into the method in the
	   String 'message'. */
	public void report_error(String message, Object info) {
   
		/* Create a StringBuffer called 'm' with the string 'Error' in it. */
		StringBuffer m = new StringBuffer("Error");
   
		/* Check if the information passed to the method is the same
		   type as the type java_cup.runtime.Symbol. */
		if (info instanceof java_cup.runtime.Symbol) {
			/* Declare a java_cup.runtime.Symbol object 's' with the
			   information in the object info that is being typecasted
			   as a java_cup.runtime.Symbol object. */
			java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
			/* Check if the line number in the input is greater or
			   equal to zero. */
			if (s.left >= 0) {                
				/* Add to the end of the StringBuffer error message
				   the line number of the error in the input. */
				m.append(" in line "+(s.left+1));
				/* Check if the column number in the input is greater
				   or equal to zero. */
				if (s.right >= 0)                    
					/* Add to the end of the StringBuffer error message
					   the column number of the error in the input. */
					m.append(", column "+(s.right+1));
			}
		}
   
		/* Add to the end of the StringBuffer error message created in
		   this method the message that was passed into this method. */
		m.append(" : "+message);
   
		/* Print the contents of the StringBuffer 'm', which contains
		   an error message, out on a line. */
		System.err.println(m);
	}
   
	/* Change the method report_fatal_error so when it reports a fatal
	   error it will display the line and column number of where the
	   fatal error occurred in the input as well as the reason for the
	   fatal error which is passed into the method in the object
	   'message' and then exit.*/
	public void report_fatal_error(String message, Object info) {
		report_error(message, info);
		System.exit(1);
	}


	

:};
   

   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  

   Terminals that have no value are listed first and then terminals
   that do have an value, in this case an integer value, are listed on
   the next line down. */
terminal           CHAR;
terminal           NEQUAL, EQUAL, GREATER, LESS, NOT;
terminal           TIMES, MINUS, DIVIDE, PLUS;
terminal           WRITE, READ;
terminal           LENGTH;
terminal           WHILE, DO;
terminal           QCHAR;
terminal           EOL;
terminal           INT;
terminal           IF, ELSE;
terminal           LPAR, RPAR, LBRACE, RBRACE, LBRACK, RBRACK;
terminal           RETURN;
terminal           ASSIGN, SEMICOLON, COMMA, DOT;
terminal           LOW, UMINUS;



terminal Integer   NUMBER;
terminal String    NAME;


non terminal Object                   program, declaration, fun_declaration,
                                      block, var_declaration,
                                      var_declarations, statements, pars;

non terminal CodeContainer            exp, statement;

non terminal TypeInfo                 type;

non terminal SymTabInfo               formal_par, lexp;
non terminal LinkedList<SymTabInfo>   formal_pars, formal_params_non_empty;
non terminal Object                   total;

/* ------------ Precedence declarations ------------------------------------ */

precedence nonassoc LOW;
precedence nonassoc ELSE;
precedence left     GREATER, LESS, EQUAL, NEQUAL;
precedence left     PLUS, MINUS;
precedence left     TIMES, DIVIDE;
precedence left     UMINUS;
precedence left     DOT, LBRACK;
/* ----------------------------Grammar Section------------------------------ */
// The total rule only serves as a rule so we can execute code at the EOF.
// Parsing the EOF token gave me some trouble so I skipped it and found this dirty solution.
// It works however, so I'm leaving it in pace. The codeblock can be used to print the
// symbol table for example after a succesful parse.

total                   ::=   program {: System.out.println("End of file");
                                         //scope.Print();
                                      :}
                          ;
program                 ::=   program declaration
						  | /* Empty */
						  ;


declaration             ::=  fun_declaration
						  |  var_declaration
						  ;

fun_declaration         ::=  type:rt NAME:n
                                                                         {: SymTabInfo sti = new SymTabInfo(n, null);
                                                                            scope.Insert(sti);
                                                                            scope = scope.NewScope();
                                                                            scope.function = sti;
                                                                         :}
                             LPAR formal_pars:ps RPAR
                                                                         {:
                                                                            // Attach the parameters to the type
                                                                            // of the function defining the current scope
                                                                            scope.function.typeInfo = new FunctionTypeInfo(rt, ps);
																		 :}
					         block
					                                                     {:
					                                                        //We close the scope and put it back as the parent.
                                                                            scope = scope.parent;
					                                                     :}
						   ;

formal_pars             ::=  formal_params_non_empty:ps                       {:
                                                                                RESULT = ps;
                                                                              :}
						  |
						  ;

formal_params_non_empty ::= formal_par:p COMMA formal_params_non_empty:ps     {:
                                                                                ps.addFirst(p);
                                                                                RESULT = ps;
                                                                              :}
                          | formal_par:p                                      {:
                                                                                RESULT = new LinkedList<SymTabInfo>(Arrays.asList(p));
                                                                              :}
                          ;

formal_par              ::= type:t NAME:n
                                                                         {:
                                                                            SymTabInfo sti = new SymTabInfo(n, t);
                                                                            // The current scope is the function itself,
                                                                            // So we add this parameter to the scope..
                                                                            scope.Insert(sti);
                                                                            RESULT = sti;
                                                                         :}
						  ;

block                   ::= LBRACE                                       {: // We open a new block so we create a new scope
                                                                            scope = scope.NewScope();
                                                                         :}
                            var_declarations statements 
                            RBRACE                                       {:
                                                                            // We close the block so scope is closed.
                                                                            scope = scope.parent;
                                                                         :}
						  ;

var_declaration         ::= type:t NAME:n SEMICOLON                      {: 
                                                                            // Create a new SymTabInfo
                                                                            // We know it's not a function
                                                                            //TODO Insert code for arrays and stuff
                                                                            SymTabInfo sti = new SymTabInfo(n, t);
                                                                            scope.Insert(sti); 
                                                                         :}
						  ;

var_declarations        ::= var_declaration var_declarations
						  | /* Nothing */
						  ;

type                    ::= INT                                          {: /* We create a new TypeInfo that holds that the value is an integer
																			 * We then attach this type to the value of 'type'
																			 * INT and FLOAT are simple types
																			 * Arrays and structs are complex types..
																			 */ 
																			RESULT = new TypeInfo(Types.INTEGER);
																		 :}
						  | CHAR
                                                                         {:
                                                                             RESULT = new TypeInfo(Types.CHAR);
                                                                         :}
						  ;

statements                  ::= statement SEMICOLON statements
							  | /* Nothing */
							  ;

statement                   ::= IF LPAR exp RPAR statement
								%prec LOW
							  | IF LPAR exp RPAR statement ELSE statement
							  | WHILE LPAR exp RPAR statement
							  | lexp:target ASSIGN exp:exp               {:
                                                                             CodeContainer cc = new CodeContainer();
                                                                             cc.AppendCode(exp.codeList);
                                                                             // Create the 3AC for the assignment   
                                                                             ThreeAddressCode tac = new ThreeAddressCode(OpCodes.A0, 
                                                                                                                         exp.place,
                                                                                                                         null,
                                                                                                                         target);
                                                                             RESULT = cc;
                                                                         :}
							  | RETURN exp
							  | block
							  | WRITE exp
							  | READ lexp
							  ;

lexp                    ::= NAME:n                                       {:
                                                                             // We should have already declared this name
                                                                             // so we simply look it up in the symbol table
                                                                             RESULT = scope.Lookup(n);
                                                                         :}
						  | lexp LBRACK exp RBRACK  // array access
						  ;

exp                     ::= exp:e1 NEQUAL  exp:e2 
						  | exp:e1 EQUAL   exp:e2 
						  | exp:e1 GREATER exp:e2 
						  | exp:e1 LESS    exp:e2 
						  | exp:e1 PLUS    exp:e2                        {:
                                                                             // Create a new variable name that will hold our minus value
                                                                             String varname = VarnameCreator.getInstance().CreateName();
                                                                             
                                                                             // Create new SymTabInfo
                                                                             // The typeinfo is the same as the original expression so 
                                                                             // we pick it out of the original CodeContainer object
                                                                             // For sum etc the type is the same as the first and second
                                                                             SymTabInfo newSti = new SymTabInfo(varname, e1.place.typeInfo);
                                                                             scope.Insert(newSti);
                                                                             CodeContainer cc = new CodeContainer();
                                                                             // The place of the variable is the SymTabInfo we just created
                                                                             cc.place = newSti;
                                                                             //We create a new 3AC that makes a minus value 
                                                                             ThreeAddressCode tac = new ThreeAddressCode(OpCodes.A2PLUS,
                                                                                                                         e1.place, e1.place, newSti);

                                                                             cc.AppendCode(e1.codeList);
                                                                             cc.AppendCode(e2.codeList);
                                                                             cc.AppendCode(tac);
                                                                             RESULT = cc;
                                                                         :}  
						  | exp:e1 MINUS   exp:e2                        {:
                                                                             // Create a new variable name that will hold our minus value
                                                                             String varname = VarnameCreator.getInstance().CreateName();
                                                                             
                                                                             // Create new SymTabInfo
                                                                             // The typeinfo is the same as the original expression so 
                                                                             // we pick it out of the original CodeContainer object
                                                                             // For sum etc the type is the same as the first and second
                                                                             SymTabInfo newSti = new SymTabInfo(varname, e1.place.typeInfo);
                                                                             scope.Insert(newSti);
                                                                             CodeContainer cc = new CodeContainer();
                                                                             // The place of the variable is the SymTabInfo we just created
                                                                             cc.place = newSti;
                                                                             //We create a new 3AC that makes a minus value 
                                                                             ThreeAddressCode tac = new ThreeAddressCode(OpCodes.A2MINUS,
                                                                                                                         e1.place, e1.place, newSti);

                                                                             cc.AppendCode(e1.codeList);
                                                                             cc.AppendCode(e2.codeList);
                                                                             cc.AppendCode(tac);
                                                                             RESULT = cc;
                                                                         :} 
						  | exp:e1 TIMES   exp:e2                        {:
                                                                             // Create a new variable name that will hold our minus value
                                                                             String varname = VarnameCreator.getInstance().CreateName();
                                                                             
                                                                             // Create new SymTabInfo
                                                                             // The typeinfo is the same as the original expression so 
                                                                             // we pick it out of the original CodeContainer object
                                                                             // For sum etc the type is the same as the first and second
                                                                             SymTabInfo newSti = new SymTabInfo(varname, e1.place.typeInfo);
                                                                             scope.Insert(newSti);
                                                                             CodeContainer cc = new CodeContainer();
                                                                             // The place of the variable is the SymTabInfo we just created
                                                                             cc.place = newSti;
                                                                             //We create a new 3AC that makes a minus value 
                                                                             ThreeAddressCode tac = new ThreeAddressCode(OpCodes.A2TIMES,
                                                                                                                         e1.place, e1.place, newSti);

                                                                             cc.AppendCode(e1.codeList);
                                                                             cc.AppendCode(e2.codeList);
                                                                             cc.AppendCode(tac);
                                                                             RESULT = cc;
                                                                         :} 
						  | exp:e1 DIVIDE  exp:e2                        {:
                                                                             // Create a new variable name that will hold our minus value
                                                                             String varname = VarnameCreator.getInstance().CreateName();
                                                                             
                                                                             // Create new SymTabInfo
                                                                             // The typeinfo is the same as the original expression so 
                                                                             // we pick it out of the original CodeContainer object
                                                                             // For sum etc the type is the same as the first and second
                                                                             SymTabInfo newSti = new SymTabInfo(varname, e1.place.typeInfo);
                                                                             scope.Insert(newSti);
                                                                             CodeContainer cc = new CodeContainer();
                                                                             // The place of the variable is the SymTabInfo we just created
                                                                             cc.place = newSti;
                                                                             //We create a new 3AC that makes a minus value 
                                                                             ThreeAddressCode tac = new ThreeAddressCode(OpCodes.A2DIV,
                                                                                                                         e1.place, e1.place, newSti);

                                                                             cc.AppendCode(e1.codeList);
                                                                             cc.AppendCode(e2.codeList);
                                                                             cc.AppendCode(tac);
                                                                             RESULT = cc;
                                                                         :} 
						  | LPAR exp:e RPAR                              {:
                                                                             // The 'place' of this variable name is 
                                                                             // the location in the symboltable.
                                                                             //TODO Nullcheck here?
                                                                             CodeContainer cc = new CodeContainer();
                                                                             cc.place = e.place;
                                                                             cc.codeList = e.codeList;
                                                                             RESULT = cc;
                                                                         :}
						  | exp LBRACK exp RBRACK
						  | MINUS exp:e                                  {:
                                                                             // Create a new variable name that will hold our minus value
                                                                             String varname = VarnameCreator.getInstance().CreateName();
                                                                             
                                                                             // Create new SymTabInfo
                                                                             // The typeinfo is the same as the original expression so 
                                                                             // we pick it out of the original CodeContainer object
                                                                             SymTabInfo newSti = new SymTabInfo(varname, e.place.typeInfo);
                                                                             scope.Insert(newSti);
                                                                             CodeContainer cc = new CodeContainer();
                                                                             // The place of the variable is the SymTabInfo we just created
                                                                             cc.place = newSti;
                                                                             //We create a new 3AC that makes a minus value 
                                                                             ThreeAddressCode tac = new ThreeAddressCode(OpCodes.A1MINUS,
                                                                                                                         e.place, null, newSti);

                                                                             cc.codeList = e.codeList;
                                                                             cc.AppendCode(tac);
                                                                             RESULT = cc;
                                                                         :}
                            %prec UMINUS  
						  | NUMBER
						  | NAME LPAR pars RPAR
						  | QCHAR
						  | NAME:n                                       {:
                                                                             // The 'place' of this variable name is 
                                                                             // the location in the symboltable.
                                                                             //TODO Nullcheck here?
                                                                             RESULT = new CodeContainer(scope.Lookup(n));
                                                                         :}
						  ;

pars                    ::= exp
						  | exp COMMA pars
						  | /* No parameters */
						  ;


